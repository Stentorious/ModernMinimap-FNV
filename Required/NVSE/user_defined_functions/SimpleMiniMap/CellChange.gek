ref	rCellExit

int iUpdate
int iTemp
int iEnterInterior
ref	rCellEnter
ref	rWorldSpace
string_var sLocation
string_var sLocationOld

begin Function {rCellExit}

	if eval Goo1.AuxVarGetFlt "*MiniMap_Travel" == 0

		rCellEnter = Player.GPC
		iEnterInterior = GetCellFlag rCellEnter 0

		; Swapped between interior/exterior cells
		if eval iEnterInterior != GetCellFlag rCellExit 0
			if eval iEnterInterior
				SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\_MapScale" (Goo1.AuxVarGetFlt "*MiniMap_INI" 0)
				SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\_Interior" 1
			else
				SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\_MapScale" (Goo1.AuxVarGetFlt "*MiniMap_INI" 1)
				SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\_Interior" 0
			endif
			iUpdate = 1

		; Changed interior cells
		elseif eval iEnterInterior
			iUpdate = 1

		; Changed world spaces
		else
			rWorldSpace = Player.GPW
			if eval rWorldSpace != Goo1.AuxVarGetRef "*MiniMap_Travel" 1
				Goo1.AuxVarSetRef "*MiniMap_Travel" rWorldSpace 1
				iUpdate = 1
			endif
		endif

		; Update map
		if eval iUpdate
			SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\_MapOriginX" -999999
			SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\_CellDeg" (ABS (GetCellNorthRotation (rCellEnter)))
			Call (CompileScript "SimpleMiniMap\MainLoop.gek")
		endif

		; Update location name
		iTemp = Goo1.AuxVarGetFlt "*MiniMap_INI" 2
		if iTemp > 0
			sLocation = Player.GetLocationName
			sLocationOld = GetUIString "HUDMainMenu\SimpleMiniMap\LocationText\string"
			if eval iTemp == 1 && sLocation != sLocationOld
				SetUIFloatGradual "HUDMainMenu\SimpleMiniMap\LocationText\alpha" 0 255 6 1
			endif
			SetUIStringAlt "HUDMainMenu\SimpleMiniMap\LocationText\string" (sLocation)
		endif

	endif

end