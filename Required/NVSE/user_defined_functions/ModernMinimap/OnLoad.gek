int iTemp
float fPosX
float fPosY
ref rDoor

begin Function {}

	; Toggle minimap visibility
	SetOnTimerStopHandler 1 (CompileScript "ModernMinimap\AuxTimer.gek") "*Minimap_Key"

	; Update player Perception
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrAV6"(Player.GetThresholdAV Perception)

	; Update cell rotation
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_CellDeg" (ABS (GetCellNorthRotation (Player.GPC)))

	; Default cell zoom
	if eval GetCellFlag (Player.GPC) 0
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapScaleLocal" (Goo1.AuxVarGetFlt "*Minimap_INI" 0)
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_Interior" 1
		iTemp = Goo1.AuxVarGetFlt "*Minimap_INI" 7
		if eval iTemp == 1
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_visible" 1
		elseif eval iTemp == 2
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_visible" 0
		endif
		if eval Goo1.AuxVarGetFlt "*Minimap_INI" 9 == 2
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapMode" 1
		endif
		fPosX = -1
		fPosY = -1
		if eval IsReference (rDoor = GetPCLastExteriorDoor)
			rDoor.GetWorldMapPosMults (fPosX) (fPosY)
		endif
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldX" fPosX
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldY" fPosY
	else
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapScaleLocal" (Goo1.AuxVarGetFlt "*Minimap_INI" 1)
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_Interior" 0
		iTemp = Goo1.AuxVarGetFlt "*Minimap_INI" 7
		if eval iTemp == 2
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_visible" 1
		elseif eval iTemp == 1
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_visible" 0
		endif
		if eval Goo1.AuxVarGetFlt "*Minimap_INI" 9 == 2
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapMode" 0
		endif
		Player.GetWorldMapPosMults (fPosX) (fPosY)
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldX" fPosX
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldY" fPosY
	endif

	; Update interface
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapOriginX" -999999
	SetUIFloatGradual "HUDMainMenu\ModernMinimap\WorldMarkers\Discover\_fade" 0

	; Update icons
	Call (CompileScript "ModernMinimap\MainLoop.gek")
	CallAfterFrames 0 (CompileScript "ModernMinimap\WorldMap\UpdateTiles.gek") 0 Player

	; Fix custom marker texture
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerLocal\Icon\_PosZ" -99999

	; Update worldspace cache
	Goo1.AuxVarSetFlt "*Minimap_Travel" 0 0
	Goo1.AuxVarSetRef "*Minimap_Travel" (Player.GPW) 1

	; Update location name
	if eval (iTemp = GetUIFloatAlt "HUDMainMenu\_Minimap+LocationText") > 0
		SetUIStringAlt "HUDMainMenu\ModernMinimap\LocationText\string" (Player.GetLocationName)
		if iTemp == 1
			SetUIFloatGradual "HUDMainMenu\ModernMinimap\LocationText\alpha" 0 255 6 1
		endif
	endif

	; Update JIP CCC avatar map
	if eval Goo1.AuxVarGetFlt "*JIPCCC_Avatars"
		Call (CompileScript "ModernMinimap\JIPCCCAvatar.gek") 0
	endif

end