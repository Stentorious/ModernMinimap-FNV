ref	rCellExit

int iUpdate
int iTemp
int iEnterInterior
float fPosX
float fPosY
ref	rCellEnter
ref	rWorldSpace
ref rDoor
string_var sLocation
string_var sLocationOld

begin Function {rCellExit}

	if eval Goo1.AuxVarGetFlt "*Minimap_Travel"
		return
	endif

	rCellEnter = Player.GPC
	iEnterInterior = GetCellFlag rCellEnter 0

	; Swapped between interior/exterior cells
	if eval iEnterInterior != GetCellFlag rCellExit 0
		if eval iEnterInterior
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapScaleLocal" (Goo1.AuxVarGetFlt "*Minimap_INI" 0)
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_Interior" 1
			iTemp = Goo1.AuxVarGetFlt "*Minimap_INI" 7
			if eval iTemp == 1
				SetUIFloatAlt "HUDMainMenu\ModernMinimap\_visible" 0
			elseif eval iTemp == 2
				SetUIFloatAlt "HUDMainMenu\ModernMinimap\_visible" 1
			endif
			if eval Goo1.AuxVarGetFlt "*Minimap_INI" 9 == 2
				SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapMode" 1
			endif
			fPosX = -1
			fPosY = -1
			if eval IsReference (rDoor = GetPCLastExteriorDoor)
				rDoor.GetWorldMapPosMults (fPosX) (fPosY)
			endif
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldX" fPosX
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldY" fPosY
		else
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapScaleLocal" (Goo1.AuxVarGetFlt "*Minimap_INI" 1)
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_Interior" 0
			iTemp = Goo1.AuxVarGetFlt "*Minimap_INI" 7
			if eval iTemp == 2
				SetUIFloatAlt "HUDMainMenu\ModernMinimap\_visible" 0
			elseif eval iTemp == 1
				SetUIFloatAlt "HUDMainMenu\ModernMinimap\_visible" 1
			endif
			if eval Goo1.AuxVarGetFlt "*Minimap_INI" 9 == 2
				SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapMode" 0
			endif
			Player.GetWorldMapPosMults (fPosX) (fPosY)
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldX" fPosX
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldY" fPosY
		endif
		iUpdate = 1

	; Changed interior cells
	elseif eval iEnterInterior
		iUpdate = 1

	; Changed world spaces
	else
		rWorldSpace = Player.GPW
		if eval rWorldSpace != Goo1.AuxVarGetRef "*Minimap_Travel" 1
			Goo1.AuxVarSetRef "*Minimap_Travel" rWorldSpace 1
			iUpdate = 1
		endif
	endif

	; Update map
	if eval iUpdate
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapOriginX" -999999
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_CellDeg" (ABS (GetCellNorthRotation (rCellEnter)))
		UnloadUIComponent "HUDMainMenu\ModernMinimap\PlayerPath"
		AddTileFromTemplate "HUDMainMenu\ModernMinimap|Minimap_Icon_Rect|PlayerPath"
		Call (CompileScript "ModernMinimap\MainLoop.gek")
		Call (CompileScript "ModernMinimap\WorldMap\UpdateTiles.gek") rCellEnter
	endif

	; Update location name
	iTemp = Goo1.AuxVarGetFlt "*Minimap_INI" 2
	if iTemp > 0
		sLocation = Player.GetLocationName
		sLocationOld = GetUIString "HUDMainMenu\ModernMinimap\LocationText\string"
		if eval iTemp == 1 && sLocation != sLocationOld
			SetUIFloatGradual "HUDMainMenu\ModernMinimap\LocationText\alpha" 0 255 6 1
		endif
		SetUIStringAlt "HUDMainMenu\ModernMinimap\LocationText\string" (sLocation)
	endif

end