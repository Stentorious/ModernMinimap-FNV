; Simple Minimap - Stentorious + xConfused

; On game restart
if eval Goo1.AuxVarGetFlt "*Minimap_Init" 0 == 0
	Goo1.AuxVarSetFlt "*Minimap_Init" 1 0

	; Aux Vars
	; "*Minimap_INI"
	; 0: ySI installation flag
	; 1: Default interior zoom
	; 2: Default exterior zoom
	; 3: Location text mode
	; 4: Actor icon mode
	; 5: Actor corpses mode
	; 6: Item icons bitmask
	; 7: Vendor icon mode
	; "*Minimap_Travel"
	; 0: Currently fast traveling flag
	; 1: Last world space
	; "*Minimap_Icons"		Icon refs
	; "*Minimap_Markers"	Quest marker refs
	; "*Minimap_Custom"		Custom marker ref

	; Check for requirements
	if eval GetNVSEVersion > 5 && GetNVSERevision > 2 && GetNVSEBeta > 2
	else
		MessageBoxEx "Simple Minimap missing requirement!%rInstall xNVSE 6.3.3+."
		return
	endif
	if eval GetPluginVersion "JohnnyGuitarNVSE" < 390
		MessageBoxEx "Simple Minimap missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
		return
	endif
	if eval GetPluginVersion "ShowOffNVSE Plugin" < 145
		MessageBoxEx "Simple Minimap missing requirement!%rInstall latest ShowOff NVSE plugin."
		return
	endif
	if eval GetPluginVersion "UI Organizer Plugin" < 230
		MessageBoxEx "Simple Minimap missing requirement!%rInstall latest User Interface Organizer plugin."
		return
	endif
	if eval GetPluginVersion "HighResLocalMaps" < 12
		MessageBoxEx "Simple Minimap missing requirement!%rInstall High Res Local Maps."
		return
	endif


	Goo1.AuxVarSetFlt "*Minimap_Init" 1 1

	; Load key binds
	int iTemp = GetINIFloat_Cached "KeyBinds:iMapVisibility" "Stentorious\SimpleMinimap.ini"
	if iTemp > 0
		SetOnKeyDownEventHandler (CompileScript "SimpleMinimap\KeyToggle.gek") 1 iTemp
	endif
	iTemp = GetINIFloat_Cached "KeyBinds:iMapRotation" "Stentorious\SimpleMinimap.ini"
	if iTemp > 0
		SetOnKeyDownEventHandler (CompileScript "SimpleMinimap\KeyRotation.gek") 1 iTemp
	endif
	iTemp = GetINIFloat_Cached "KeyBinds:iMapZoomIn" "Stentorious\SimpleMinimap.ini"
	if iTemp > 0
		SetOnKeyDownEventHandler (CompileScript "SimpleMinimap\KeyZoomIn.gek") 1 iTemp
	endif
	iTemp = GetINIFloat_Cached "KeyBinds:iMapZoomOut" "Stentorious\SimpleMinimap.ini"
	if iTemp > 0
		SetOnKeyDownEventHandler (CompileScript "SimpleMinimap\KeyZoomOut.gek") 1 iTemp
	endif

	; Load map settings
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_corner" (GetINIFloat_Cached "Map:iPosition" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_x" (GetINIFloat_Cached "Map:fOffsetX" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_y" (GetINIFloat_Cached "Map:fOffsetY" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_TileScale" (GetINIFloat_Cached "Map:fSize" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\Background\alpha" ((GetINIFloat_Cached "Map:fBackgroundAlpha" "Stentorious\SimpleMinimap.ini") * 255)
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\MapRender\_brightness" (((GetINIFloat_Cached "Map:fRenderBrightness" "Stentorious\SimpleMinimap.ini") * 255) - 255)
	Goo1.AuxVarSetFlt "*Minimap_INI" (GetMaxOf 0.5 (GetMinOf 1.5 (GetINIFloat_Cached "Map:fInteriorZoom" "Stentorious\SimpleMinimap.ini"))) 0
	Goo1.AuxVarSetFlt "*Minimap_INI" (GetMaxOf 0.5 (GetMinOf 1.5 (GetINIFloat_Cached "Map:fExteriorZoom" "Stentorious\SimpleMinimap.ini"))) 1
	; Compass rose cross
	if eval GetINIFloat_Cached "Map:bCompassRose" "Stentorious\SimpleMinimap.ini"
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\Headline_HC\visible" 1
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\Headline_VC\visible" 1
	endif
	; Cardinal direction text
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\NorthText\visible" (eval GetINIFloat_Cached "Map:bNorthText" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\SouthText\visible" (eval GetINIFloat_Cached "Map:bSouthText" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\EastText\visible" (eval GetINIFloat_Cached "Map:bEastText" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\WestText\visible" (eval GetINIFloat_Cached "Map:bWestText" "Stentorious\SimpleMinimap.ini")
	; Scan animation
	if eval GetINIFloat_Cached "Map:bScanEffect" "Stentorious\SimpleMinimap.ini"
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\Scan\visible" 1
		iTemp = GetUIFloatAlt "HUDMainMenu\SimpleMinimap\_width"
		SetUIFloatGradual "HUDMainMenu\SimpleMinimap\Scan\brightness" 153 255 1 2
		SetUIFloatGradual "HUDMainMenu\SimpleMinimap\Scan\y" (iTemp * -2) (iTemp * 3) 10 3
	endif
	; Combat indicator
	if eval (GetINIFloat_Cached "Map:bCombatIndicator" "Stentorious\SimpleMinimap.ini") == 0
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_systemcolor" 1
	endif
	; Default rotation mode
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_RotationMode" (eval GetINIFloat_Cached "Map:iRotationMode" "Stentorious\SimpleMinimap.ini")
	; Location text
	iTemp = GetINIFloat_Cached "Map:iLocationText" "Stentorious\SimpleMinimap.ini"
	Goo1.AuxVarSetFlt "*Minimap_INI" iTemp 2
	if iTemp > 0
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\LocationText\visible" 1
	endif

	; Load icon settings
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_IconScale" (GetINIFloat_Cached "Icons:fSize" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_Drop" (GetINIFloat_Cached "Icons:bDropShadow" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_Height" (eval GetINIFloat_Cached "Icons:bHeightIndicator" "Stentorious\SimpleMinimap.ini")
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_Dist" ((GetMaxOf 5 (GetMinOf 100 (GetINIFloat_Cached "Icons:fRadius" "Stentorious\SimpleMinimap.ini"))) * 69.99104)
	if eval GetINIFloat_Cached "Icons:bPerceptionCalc" "Stentorious\SimpleMinimap.ini"
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_DistMode" 1
		SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fOldValue, float fNewValue} => SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_PlrAV6" (GetMaxOf 1 (GetMinOf 10 (Player.GetAV Perception)))) 0 6
	endif
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_Fade" (eval GetINIFloat_Cached "Icons:bFadeAlpha" "Stentorious\SimpleMinimap.ini")

	; Actor settings
	if eval GetINIFloat_Cached "Actors:bHostileColor" "Stentorious\SimpleMinimap.ini"
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_Color2" 2
	endif
	Goo1.AuxVarSetFlt "*Minimap_INI" (GetMaxOf 0 (GetMinOf 3 (GetINIFloat_Cached "Actors:iMode" "Stentorious\SimpleMinimap.ini"))) 3
	Goo1.AuxVarSetFlt "*Minimap_INI" (eval GetINIFloat_Cached "Actors:bHideEmptyCorpses" "Stentorious\SimpleMinimap.ini") 4

	; Marker alpha pulse
	SetUIFloatGradual "HUDMainMenu\SimpleMinimap\_AlphaPulse" -1.5 0.75 3 2

	; Color mode (EXPERIMENTAL)
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_ColorMode" (eval GetINIFloat_Cached "Developer:bColorRender" "Stentorious\SimpleMinimap.ini")

	CloseFileSO "Stentorious\SimpleMinimap.ini" 0

	; Add custom quest marker template
	AddTileFromTemplate "HUDMainMenu\SimpleMinimap|Minimap_Marker_Template|CustomMarker"
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\CustomMarker\depth" 200
	SetUIStringAlt "HUDMainMenu\SimpleMinimap\CustomMarker\_filename_0" "Interface\Stentorious\SimpleMinimap\MarkerPlayerNeutral.dds"
	SetUIStringAlt "HUDMainMenu\SimpleMinimap\CustomMarker\_filename_1" "Interface\Stentorious\SimpleMinimap\MarkerPlayerBelow.dds"
	SetUIStringAlt "HUDMainMenu\SimpleMinimap\CustomMarker\_filename_2" "Interface\Stentorious\SimpleMinimap\MarkerPlayerAbove.dds"

	; Init event handlers
	SetGameMainLoopCallback (CompileScript "SimpleMinimap\MainLoop.gek") 1 15 1
	SetGameMainLoopCallback (CompileScript "SimpleMinimap\OnRender.gek") 1 1 1
	SetEventHandler "OnCellExit" (CompileScript "SimpleMinimap\CellChange.gek")
	SetOnFastTravelEventHandler (begin Function {ref rMapMarker}
		Goo1.AuxVarSetFlt "*Minimap_Travel" 1
		CallAfterFrames 0 (CompileScript "SimpleMinimap\FastTravel.gek") 0 (Player.GPC)
	end) 1

endif

; On game load
if eval Goo1.AuxVarGetFlt "*Minimap_Init" 1

	; Reset Minimap rect
	UnloadUIComponent "HUDMainMenu\SimpleMinimap\Icons"
	UnloadUIComponent "HUDMainMenu\SimpleMinimap\Markers"
	AddTileFromTemplate "HUDMainMenu\SimpleMinimap|Minimap_Icon_Rect|Icons"
	AddTileFromTemplate "HUDMainMenu\SimpleMinimap|Minimap_Icon_Rect|Markers"

	; Update player Perception
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_PlrAV6" (GetMaxOf 1 (GetMinOf 10 (Player.GetAV Perception)))

	; Update cell rotation
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_CellDeg" (ABS (GetCellNorthRotation (Player.GPC)))

	; Default cell zoom
	if eval GetCellFlag (Player.GPC) 0
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_MapScale" (Goo1.AuxVarGetFlt "*Minimap_INI" 0)
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_Interior" 1
	else
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_MapScale" (Goo1.AuxVarGetFlt "*Minimap_INI" 1)
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_Interior" 0
	endif

	; Update render
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_MapOriginX" -999999

	; Update icons
	Call (CompileScript "SimpleMinimap\MainLoop.gek")

	; Update worldspace cache
	Goo1.AuxVarSetFlt "*Minimap_Travel" 0 0
	Goo1.AuxVarSetRef "*Minimap_Travel" (Player.GPW) 1

	; Update location name
	iTemp = Goo1.AuxVarGetFlt "*Minimap_INI" 2
	if iTemp > 0
		CallAfterFrames 0 ({} => SetUIStringAlt "HUDMainMenu\SimpleMinimap\LocationText\string" (Player.GetLocationName))
		if iTemp == 1
			SetUIFloatGradual "HUDMainMenu\SimpleMinimap\LocationText\alpha" 0 255 6 1
		endif
	endif

endif