int	iDist
int	iTemp
int	iDepth
int iSize
int iIndex
int iType
ref rIcon
array_var aRefs
array_var aTemp
array_var aFollowers
array_var aMarkerRefs

begin Function {}

	; Update player info
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_PlrDetect" (GetPCDetectionState)

	; Reset tracked refs
	aMarkerRefs = Ar_Construct "array"
	Goo1.AuxVarErase "*Minimap_Doors"
	Goo1.AuxVarErase "*Minimap_Actors"
	Goo1.AuxVarErase "*Minimap_Followers"
	Goo1.AuxVarErase "*Minimap_Markers"

	; Get global settings
	iDist = GetUIFloatAlt "HUDMainMenu\SimpleMinimap\_Dist"

	; Door refs
	aRefs = Player.GetRefs 28 2 1 (iDist * 2)
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.IsLoadDoor
				Goo1.AuxVarSetRef "*Minimap_Doors" rIcon -1
			endif
		loop
	endif

	; Actor refs
	if eval (iTemp = Goo1.AuxVarGetFlt "*Minimap_INI" 3) > 0
		if iTemp == 2
			aRefs = Player.GetRefs 43 2 1 iDist
		else
			aRefs = Player.GetRefs 42 2 1 iDist
			if iTemp == 3
				aTemp = Player.GetRefs 43 2 1 iDist
				ar_Cat aRefs aTemp
			endif
		endif

		if eval (iSize = Ar_Size aRefs) > 0

			; Build array of followers
			aFollowers = Player.GetFollowers
			aTemp = Player.GetTeammates
			ar_Cat aFollowers aTemp
			aFollowers = Ar_Unique aFollowers

			; Checks if actor is a follow and assigns them to the respective aux
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.IsActorsAIOff == 0 && rIcon.IsActorInvisibleToPlayer == 0
					if eval (Ar_Find rIcon aFollowers) == Ar_BadNumericIndex
						Goo1.AuxVarSetRef "*Minimap_Actors" rIcon -1
					else
						Goo1.AuxVarSetRef "*Minimap_Followers" rIcon -1
					endif
				endif
			loop
		endif
	endif

	; Quest objective markers
	aRefs = GetCurrentQuestObjectiveTeleportLinks
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize][0]
			if eval IsFormValid rIcon && IsReference rIcon
				Ar_Append aMarkerRefs rIcon
			endif
		loop
		aMarkerRefs = Ar_Unique aMarkerRefs
	endif

	; Update custom marker tile
	if eval IsFormValid (rIcon = GetCustomMapMarker)
		Goo1.AuxVarSetRef "*Minimap_Custom" rIcon
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\CustomMarker\visible" 1
	else
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\CustomMarker\visible" 0
	endif

	; Update door icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\SimpleMinimap\Doors\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Doors") - iIndex
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\SimpleMinimap\Doors|Minimap_Icon_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\SimpleMinimap\Doors\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\SimpleMinimap\Doors\Icon:0"
		loop
	endif

	; Update actor icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\SimpleMinimap\Actors\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Actors") - iIndex
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\SimpleMinimap\Actors|Minimap_Icon_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\SimpleMinimap\Actors\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\SimpleMinimap\Actors\Icon:0"
		loop
	endif

	; Update follower icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\SimpleMinimap\Followers\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Followers") - iIndex
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\SimpleMinimap\Followers|Minimap_Icon_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\SimpleMinimap\Followers\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\SimpleMinimap\Followers\Icon:0"
		loop
	endif

	; Update quest marker tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\SimpleMinimap\Markers\childcount")
	iSize = (Ar_Size aMarkerRefs) - iIndex
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\SimpleMinimap\Markers|Minimap_Marker_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\SimpleMinimap\Markers\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\SimpleMinimap\Markers\Icon:0"
		loop
	endif
	Goo1.AuxVarSetFromArr "*Minimap_Markers" aMarkerRefs

	aRefs = aTemp = aFollowers = aMarkerRefs = ar_Null

end