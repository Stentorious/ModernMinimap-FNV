int	iDist
int	iTemp
int	iDepth
int iSize
int iIndex
int iType
ref rIcon
array_var aRefs
array_var aTemp
array_var aIconRefs
array_var aMarkerRefs

begin Function {}

	; Update player info
	SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\_PlrDetect" (GetPCDetectionState)

	; Reset tracked refs
	aIconRefs = Ar_Construct "array"
	aMarkerRefs = Ar_Construct "array"
	Goo1.AuxVarErase "*MiniMap_Icons"
	Goo1.AuxVarErase "*MiniMap_Markers"

	; Get global settings
	iDist = GetUIFloatAlt "HUDMainMenu\SimpleMiniMap\_Dist"

	; Door refs
	aRefs = Player.GetRefs 28 2 1 iDist

	; Actor refs
	iTemp = Goo1.AuxVarGetFlt "*MiniMap_INI" 3
	if eval iTemp > 0
		if iTemp == 2
			aTemp = Player.GetRefs 43 2 1 iDist
			ar_Cat aRefs aTemp
		else
			aTemp = Player.GetRefs 42 2 1 iDist
			ar_Cat aRefs aTemp
			if iTemp == 3
				aTemp = Player.GetRefs 43 2 1 iDist
				ar_Cat aRefs aTemp
			endif
		endif
	endif

	; Cache all valid icon refs
	iSize = Ar_Size aRefs
	if eval iSize > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D
				iType = rIcon.GetType
				; Actor refs
				if eval (iType == 42 || iType == 43) && rIcon.IsActorsAIOff == 0 && rIcon.IsActorInvisibleToPlayer == 0
					Ar_Append aIconRefs rIcon
				; Door refs
				elseif eval iType == 28 && rIcon.IsLoadDoor
					Ar_Append aIconRefs rIcon
				endif
			endif
		loop
		Goo1.AuxVarSetFromArr "*MiniMap_Icons" aIconRefs
	endif

	; Update icon tile count
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\SimpleMiniMap\Icons\childcount")
	iSize = (Ar_Size aIconRefs) - iIndex

	; Add tile
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\SimpleMiniMap\Icons|MiniMap_Icon_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\Icons\Icon:0\_DepthMod" (iIndex += 1)
		loop
	; Remove tile
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\SimpleMiniMap\Icons\Icon:0"
		loop
	endif

	; Quest objective markers
	aRefs = GetCurrentQuestObjectiveTeleportLinks
	iSize = Ar_Size aRefs
	if eval iSize > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize][0]
			if eval IsFormValid rIcon && IsReference rIcon && (Ar_Find rIcon aMarkerRefs) == Ar_BadNumericIndex
				Ar_Append aMarkerRefs rIcon
			endif
		loop
	endif
	Goo1.AuxVarSetFromArr "*MiniMap_Markers" aMarkerRefs

	; Update marker tile count
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\SimpleMiniMap\Markers\childcount")
	iSize = (Ar_Size aMarkerRefs) - iIndex

	; Add tile
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\SimpleMiniMap\Markers|MiniMap_Marker_Template|Marker"
			SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\Markers\Marker:0\_DepthMod" (iIndex += 1)
		loop
	; Remove tile
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\SimpleMiniMap\Markers\Marker:0"
		loop
	endif

	; Player marker
	rIcon = GetCustomMapMarker
	if eval IsFormValid rIcon && IsReference rIcon
		Goo1.AuxVarSetRef "*MiniMap_Custom" rIcon
		SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\CustomMarker\visible" 1
	else
		SetUIFloatAlt "HUDMainMenu\SimpleMiniMap\CustomMarker\visible" 0
	endif

	aRefs = aTemp = aIconRefs = aMarkerRefs = ar_Null

end