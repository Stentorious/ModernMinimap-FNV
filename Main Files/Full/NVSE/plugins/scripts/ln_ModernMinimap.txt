; Modern Minimap - Stentorious + xConfused

; On game load

; Reset icon arrays
Goo1.AuxVarErase "*Minimap_Doors"
Goo1.AuxVarErase "*Minimap_Basic"
Goo1.AuxVarErase "*Minimap_Actors"
Goo1.AuxVarErase "*Minimap_Followers"
Goo1.AuxVarErase "*Minimap_Markers"

; Reset Minimap rect
UnloadUIComponent "HUDMainMenu\ModernMinimap\Doors"
UnloadUIComponent "HUDMainMenu\ModernMinimap\Basic"
UnloadUIComponent "HUDMainMenu\ModernMinimap\Actors"
UnloadUIComponent "HUDMainMenu\ModernMinimap\Followers"
UnloadUIComponent "HUDMainMenu\ModernMinimap\QuestLocal"
UnloadUIComponent "HUDMainMenu\ModernMinimap\PlayerPath"
UnloadUIComponent "HUDMainMenu\ModernMinimap\WorldMarkers"
UnloadUIComponent "HUDMainMenu\ModernMinimap\QuestWorld"
AddTileFromTemplate "HUDMainMenu\ModernMinimap|Minimap_Icon_Rect|Doors"
AddTileFromTemplate "HUDMainMenu\ModernMinimap|Minimap_Icon_Rect|Basic"
AddTileFromTemplate "HUDMainMenu\ModernMinimap|Minimap_Icon_Rect|Actors"
AddTileFromTemplate "HUDMainMenu\ModernMinimap|Minimap_Icon_Rect|Followers"
AddTileFromTemplate "HUDMainMenu\ModernMinimap|Minimap_Quest_Rect|QuestLocal"
AddTileFromTemplate "HUDMainMenu\ModernMinimap|Minimap_Icon_Rect|PlayerPath"
AddTileFromTemplate "HUDMainMenu\ModernMinimap|WorldMap_Icon_Rect|WorldMarkers"
AddTileFromTemplate "HUDMainMenu\ModernMinimap|WorldMap_Quest_Rect|QuestWorld"

CallAfterFrames 0 (CompileScript "ModernMinimap\OnLoad.gek")

; On game restart
if eval Goo1.AuxVarGetFlt "*Minimap_Init"
	return
endif
Goo1.AuxVarSetFlt "*Minimap_Init" 1

; Aux Vars
; "*Minimap_INI"
; 0: ySI installation flag
; 1: Default interior zoom
; 2: Default exterior zoom
; 3: Location text mode
; 4: Actor icon mode
; 5: Actor corpses mode
; 6: Item icons bitmask
; 7: Vendor icon mode
; "*Minimap_Travel"
; 0: Currently fast traveling flag
; 1: Last world space
; "*Minimap_Icons"		Icon refs
; "*Minimap_Markers"	Quest marker refs

; Check for requirements
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "Modern Minimap missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if eval GetPluginVersion "JohnnyGuitarNVSE" < 390
	MessageBoxEx "Modern Minimap missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if eval GetPluginVersion "ShowOffNVSE Plugin" < 145
	MessageBoxEx "Modern Minimap missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if eval GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "Modern Minimap missing requirement!%rInstall latest User Interface Organizer plugin."
	return
endif
if eval GetPluginVersion "HighResLocalMaps" < 12
	MessageBoxEx "Modern Minimap missing requirement!%rInstall High Res Local Maps."
	return
endif
if eval GetPluginVersion "Engine Optimizations" < 100
	MessageBoxEx "Modern Minimap missing requirement!%rInstall lStewieAl's Engine Optimizations."
	return
endif
if eval IsModLoaded "JIP MiniMap.esp"
	MessageBoxEx "Modern Minimap conflict detected!%rUninstall JIP Minimap."
	return
endif
if GetPluginVersion "yUI" < 140
	MessageBoxEx "Modern Minimap missing requirement!%rInstall latest yUI - User Ynterface plugin."
	return
endif
if FileExists "menus\ySI\ySI.xml" == 0
	MessageBoxEx "Modern Minimap missing requirement!%rInstall latest ySI - Sorting Ycons."
	return
endif
if eval GetINIFloat "Main:iResScale" "..\NVSE\Plugins\HighResLocalMaps.ini" < 2
	MessageBoxEx "Modern Minimap overwrite detected!%rLoad after High Res Local Maps."
endif

; Load key binds
Goo1.AuxVarSetFlt "*Minimap_Key" (GetINIFloat_Cached "KeyBinds:iMapVisibility" "Stentorious\ModernMinimap.ini" 50) 0
if eval (int iTemp = Goo1.AuxVarGetFlt "*Minimap_Key" 0) > 0
	SetOnKeyDownEventHandler (CompileScript "ModernMinimap\InputHandler.gek") 1 iTemp
	SetOnKeyUpEventHandler (CompileScript "ModernMinimap\KeyToggle.gek") 1 iTemp
endif
Goo1.AuxVarSetFlt "*Minimap_Key" (GetINIFloat_Cached "KeyBinds:iMapRotation" "Stentorious\ModernMinimap.ini" 207) 1
if eval (iTemp = Goo1.AuxVarGetFlt "*Minimap_Key" 1) > 0
	SetOnKeyDownEventHandler (CompileScript "ModernMinimap\InputHandler.gek") 1 iTemp
endif
Goo1.AuxVarSetFlt "*Minimap_Key" (GetINIFloat_Cached "KeyBinds:iMapZoomIn" "Stentorious\ModernMinimap.ini" 201) 2
if eval (iTemp = Goo1.AuxVarGetFlt "*Minimap_Key" 2) > 0
	SetOnKeyDownEventHandler (CompileScript "ModernMinimap\InputHandler.gek") 1 iTemp
endif
Goo1.AuxVarSetFlt "*Minimap_Key" (GetINIFloat_Cached "KeyBinds:iMapZoomOut" "Stentorious\ModernMinimap.ini" 209) 3
if eval (iTemp = Goo1.AuxVarGetFlt "*Minimap_Key" 3) > 0
	SetOnKeyDownEventHandler (CompileScript "ModernMinimap\InputHandler.gek") 1 iTemp
endif
Goo1.AuxVarSetFlt "*Minimap_Key" (GetINIFloat_Cached "KeyBinds:iMarkerVisibility" "Stentorious\ModernMinimap.ini" 199) 4
if eval (iTemp = Goo1.AuxVarGetFlt "*Minimap_Key" 4) > 0
	SetOnKeyDownEventHandler (CompileScript "ModernMinimap\InputHandler.gek") 1 iTemp
endif

; Load map settings
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_corner" (GetINIFloat_Cached "Map:iPosition" "Stentorious\ModernMinimap.ini" 1)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_x" (GetINIFloat_Cached "Map:fOffsetX" "Stentorious\ModernMinimap.ini" 24)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_y" (GetINIFloat_Cached "Map:fOffsetY" "Stentorious\ModernMinimap.ini" 24)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_TileScale" (GetINIFloat_Cached "Map:fSize" "Stentorious\ModernMinimap.ini" 1)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\Background\alpha" ((GetINIFloat_Cached "Map:fBackgroundAlpha" "Stentorious\ModernMinimap.ini" 0.8) * 255)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_background_padding" (GetINIFloat_Cached "Map:fBackgroundPadding" "Stentorious\ModernMinimap.ini" 8)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_line_thickness" (GetINIFloat_Cached "Map:fLineThickness" "Stentorious\ModernMinimap.ini" 2)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\MapRender\_brightness" (((GetINIFloat_Cached "Map:fRenderBrightness" "Stentorious\ModernMinimap.ini" 2) * 255) - 255)
Goo1.AuxVarSetFlt "*Minimap_INI" (Clamp (GetINIFloat_Cached "Map:fInteriorZoom" "Stentorious\ModernMinimap.ini" 1.25) 0.5 1.5) 0
Goo1.AuxVarSetFlt "*Minimap_INI" (Clamp (GetINIFloat_Cached "Map:fExteriorZoom" "Stentorious\ModernMinimap.ini" 0.75) 0.5 1.5) 1
; Compass rose cross
SetUIFloatAlt "HUDMainMenu\ModernMinimap\Headline_VC\_visible" (eval GetINIFloat_Cached "Map:bCompassRose" "Stentorious\ModernMinimap.ini" 1)
; Cardinal direction text
SetUIFloatAlt "HUDMainMenu\ModernMinimap\NorthText\visible" (eval GetINIFloat_Cached "Map:bNorthText" "Stentorious\ModernMinimap.ini" 1)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\SouthText\visible" (eval GetINIFloat_Cached "Map:bSouthText" "Stentorious\ModernMinimap.ini" 1)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\EastText\visible" (eval GetINIFloat_Cached "Map:bEastText" "Stentorious\ModernMinimap.ini" 1)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\WestText\visible" (eval GetINIFloat_Cached "Map:bWestText" "Stentorious\ModernMinimap.ini" 1)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_CompassTextScale" (GetINIFloat_Cached "Map:fCompassTextSize" "Stentorious\ModernMinimap.ini" 1)
; Scan animation
if eval GetINIFloat_Cached "Map:bScanEffect" "Stentorious\ModernMinimap.ini" 1
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\Scan\visible" 1
	iTemp = GetUIFloatAlt "HUDMainMenu\ModernMinimap\_width"
	SetUIFloatGradual "HUDMainMenu\ModernMinimap\Scan\brightness" 153 255 1 2
	SetUIFloatGradual "HUDMainMenu\ModernMinimap\Scan\y" (iTemp * -2) (iTemp * 3) 10 3
endif
; Combat indicator
if eval (GetINIFloat_Cached "Map:bCombatIndicator" "Stentorious\ModernMinimap.ini" 1) == 0
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_systemcolor" 1
endif
; Default rotation mode
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_RotationMode" (eval GetINIFloat_Cached "Map:iRotationMode" "Stentorious\ModernMinimap.ini" 1)
; Location text
iTemp = GetINIFloat_Cached "Map:iLocationText" "Stentorious\ModernMinimap.ini" 1
Goo1.AuxVarSetFlt "*Minimap_INI" iTemp 2
if iTemp > 0
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\LocationText\visible" 1
endif
; Radiation Meter
iTemp = GetINIFloat_Cached "Map:iOverrideRadMeter" "Stentorious\ModernMinimap.ini" 1
if iTemp == 1 && IsModLoaded "TheHUDEditor.esm" == 0
	SetUIFloatAlt "HUDMainMenu\RadiationMeter\x" (GetUIFloatAlt "HUDMainMenu\ModernMinimap\x" - GetUIFloatAlt "HUDMainMenu\RadiationMeter\width" - 20)
	SetUIFloatAlt "HUDMainMenu\RadWrap\RadiationMeter\x" (GetUIFloatAlt "HUDMainMenu\ModernMinimap\x" - GetUIFloatAlt "HUDMainMenu\RadWrap\RadiationMeter\width" - 40)
elseif iTemp == 2
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_RadIndicator" 1
	ModUIFloat "HUDMainMenu\RadiationMeter\y" -3000
	SetUIFloatAlt "HUDMainMenu\RadWrap\visible" 0
endif

; Load icon settings
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_IconScale" (GetINIFloat_Cached "Icons:fSize" "Stentorious\ModernMinimap.ini" 1)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_Height" (eval GetINIFloat_Cached "Icons:bHeightIndicator" "Stentorious\ModernMinimap.ini" 1)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_QuestHeight" (eval GetINIFloat_Cached "Icons:bQuestHeightIndicator" "Stentorious\ModernMinimap.ini" 0)
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_Dist" ((Clamp (GetINIFloat_Cached "Icons:fRadius" "Stentorious\ModernMinimap.ini" 15) 5 100) * 69.99104)
if eval GetINIFloat_Cached "Icons:bPerceptionCalc" "Stentorious\ModernMinimap.ini" 0
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_DistMode" 1
	SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fOldValue, float fNewValue} => SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrAV6" (Player.GetThresholdAV Perception)) 0 6
endif
SetUIFloatAlt "HUDMainMenu\ModernMinimap\_Fade" (eval GetINIFloat_Cached "Icons:bFadeAlpha" "Stentorious\ModernMinimap.ini" 1)

; Actor settings
if eval GetINIFloat_Cached "Actors:bHostileColor" "Stentorious\ModernMinimap.ini" 1
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_Color2" 2
endif
Goo1.AuxVarSetFlt "*Minimap_INI" (Clamp (GetINIFloat_Cached "Actors:iMode" "Stentorious\ModernMinimap.ini" 3) 0 3) 3
Goo1.AuxVarSetFlt "*Minimap_INI" (Clamp (GetINIFloat_Cached "Actors:iCorpse" "Stentorious\ModernMinimap.ini" 1) 0 2) 4

; Teammate color
string_var sINI = GetINIString_Cached "Companions:sColor" "Stentorious\ModernMinimap.ini" "2ECEFF"
if eval (int iLength = Sv_Length sINI) == 6 && TestExpr (ToNumber sINI 1 1)
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_red_2" (ToNumber (sINI[0:1]) 1)
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_green_2" (ToNumber (sINI[2:3]) 1)
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_blue_2" (ToNumber (sINI[4:5]) 1)
elseif eval iLength == 1 && TestExpr (ToNumber sINI 0 1)
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_systemcolor_2" (ToNumber sINI)
endif
sv_Destruct sINI

; JIP CCC avatar support
if eval (GetINIFloat_Cached "Companions:bJIPCCC" "Stentorious\ModernMinimap.ini" 1) && FileExists "textures\jazzisparis\ccc\avatar_male.dds"
	Goo1.AuxVarSetFlt "*Minimap_INI" 1 5
	SetOnMenuCloseEventHandler (CompileScript "ModernMinimap\JIPCCCAvatar.gek") 1 1009
	if eval IsModLoaded "JIP Companions Command & Control.esp"
		SetOnMenuOpenEventHandler ({int iMenuID} => Goo1.AuxVarSetFlt "*JIPCCC_Force" (GetVariable "bConfirm" (EditorIDToFormID "JIPCCCMain"))) 1 1001
		SetOnMenuCloseEventHandler ({int iMenuID} => CallAfter 0.1 (CompileScript "ModernMinimap\JIPCCCAvatar.gek") 0 1001) 1 1001
	endif
else
	Goo1.AuxVarSetFlt "*Minimap_INI" 0 5
endif
if eval GetINIFloat_Cached "Companions:bJIPCCCColor" "Stentorious\ModernMinimap.ini" 0
	Goo1.AuxVarSetFlt "*Minimap_INI" 2 6
else
	Goo1.AuxVarSetFlt "*Minimap_INI" 3 6
endif

; Post release updates
Goo1.AuxVarSetFlt "*Minimap_INI" (GetINIFloat_Cached "Map:iCellVisibility" "Stentorious\ModernMinimap.ini" 0) 7
Goo1.AuxVarSetFlt "*Minimap_INI" (eval GetINIFloat_Cached "Icons:bPlayerPath" "Stentorious\ModernMinimap.ini" 1) 8
Goo1.AuxVarSetFlt "*Minimap_INI" (GetINIFloat_Cached "Map:iMapMode" "Stentorious\ModernMinimap.ini" 2) 9
if eval Goo1.AuxVarGetFlt "*Minimap_INI" 9 == 1
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapMode" 0
endif

; Marker alpha pulse
SetUIFloatGradual "HUDMainMenu\ModernMinimap\_IconQuestPulse" -1.5 0.75 3 2

; Radiation alpha pulse
SetUIFloatGradual "HUDMainMenu\ModernMinimap\_RadiationPulse" 0.33 1 1 2

; Color mode (EXPERIMENTAL)
;SetUIFloatAlt "HUDMainMenu\ModernMinimap\_ColorMode" (eval GetINIFloat_Cached "Developer:bColorRender" "Stentorious\ModernMinimap.ini")

int iDeadMoney = (eval IsModLoaded "DeadMoney.esm")
int iHonestHearts = (eval IsModLoaded "HonestHearts.esm")
int iOldWorldBlues = (eval IsModLoaded "OldWorldBlues.esm")
int iLonesomeRoad = (eval IsModLoaded "LonesomeRoad.esm")
int iTaleOfTwoWastelands = (eval IsModLoaded "TaleOfTwoWastelands.esm")

; Assign keywords (Crafting stations)
if eval GetINIFloat_Cached "Objects:bCraftingStation" "Stentorious\ModernMinimap.ini" 1
	AssignKeyword WorkBench "MinimapIcon"
	AssignKeyword ReloadingBench "MinimapIcon"
	AssignKeyword CampfireCrafting01 "MinimapIcon"
	AssignKeyword CampfireCrafting02 "MinimapIcon"
	AssignKeyword CampfireCrafting03 "MinimapIcon"
	AssignKeyword CampfireCrafting04 "MinimapIcon"
	AssignKeyword VCG03CampfireCrafting03 "MinimapIcon"
	if eval iDeadMoney
		AssignKeyword (EditorIDToFormID "NVDLC01CraftingOven") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01VendingArtDeco") "MinimapIcon"
	endif
	if eval iHonestHearts
		AssignKeyword (EditorIDToFormID "NVDLC02Campfire01") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC02Campfire02") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC02CraftingOven01") "MinimapIcon"
	endif
	if eval iOldWorldBlues
		AssignKeyword (EditorIDToFormID "NVDLC03HotPlate") "MinimapIcon"
	endif
	if eval iTaleOfTwoWastelands
		AssignKeyword (EditorIDToFormID "TTWDLC05ControlWB") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "TTWDLC05ControlRB") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "TTWCampGrillCrafting01") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "TTWGrillBBQDirtyCrafting") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "TTWGrillBBQCleanCrafting") "MinimapIcon"
	endif
endif

iTemp = 0

; Assign keywords (Skill books)
if eval GetINIFloat_Cached "Objects:bSkillBook" "Stentorious\ModernMinimap.ini" 1
	AssignKeyword BookSkillBarter "MinimapIcon"
	AssignKeyword BookSkillBigGuns "MinimapIcon"
	AssignKeyword BookSkillEnergyWeapons "MinimapIcon"
	AssignKeyword BookSkillExplosives "MinimapIcon"
	AssignKeyword BookSkillGuns "MinimapIcon"
	AssignKeyword BookSkillLockpicking "MinimapIcon"
	AssignKeyword BookSkillMedicine "MinimapIcon"
	AssignKeyword BookSkillMelee "MinimapIcon"
	AssignKeyword BookSkillRepair "MinimapIcon"
	AssignKeyword BookSkillScience "MinimapIcon"
	AssignKeyword BookSkillSneak "MinimapIcon"
	AssignKeyword BookSkillSpeech "MinimapIcon"
	AssignKeyword BookSkillSurvival "MinimapIcon"
	AssignKeyword BookSkillUnarmed "MinimapIcon"
	if eval iTaleOfTwoWastelands
		AssignKeyword (EditorIDToFormID "DLC04GhoulEcologyBook") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "DLC04MirelurkEcologyBook") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "TTWBookSkillBarterVault101d") "MinimapIcon"
	endif
	iTemp += 1
endif

; Assign keywords (Skill magazines)
if eval GetINIFloat_Cached "Objects:bSkillMagazine" "Stentorious\ModernMinimap.ini" 1
	AssignKeyword MagazineNVBarter "MinimapIcon"
	AssignKeyword MagazineNVCritical "MinimapIcon"
	AssignKeyword MagazineNVEnergyWeapons "MinimapIcon"
	AssignKeyword MagazineNVExplosives "MinimapIcon"
	AssignKeyword MagazineNVGuns "MinimapIcon"
	AssignKeyword MagazineNVLockpick "MinimapIcon"
	AssignKeyword MagazineNVMedicine "MinimapIcon"
	AssignKeyword MagazineNVMeleeWeapons "MinimapIcon"
	AssignKeyword MagazineNVRepair "MinimapIcon"
	AssignKeyword MagazineNVScience "MinimapIcon"
	AssignKeyword MagazineNVSneak "MinimapIcon"
	AssignKeyword MagazineNVSpeech "MinimapIcon"
	AssignKeyword MagazineNVSurvival "MinimapIcon"
	AssignKeyword MagazineNVUnarmed "MinimapIcon"
endif

; Assign keywords (Collectables)
if eval GetINIFloat_Cached "Objects:bCollectable" "Stentorious\ModernMinimap.ini" 1
	AssignKeyword NVStarBottleCap "MinimapIcon"
	AssignKeyword SnowglobeFortmormon "MinimapIcon"
	AssignKeyword SnowglobeMtCharleston "MinimapIcon"
	AssignKeyword SnowglobeHooverDam "MinimapIcon"
	AssignKeyword SnowglobeGoodsprings "MinimapIcon"
	AssignKeyword SnowglobeTestSite "MinimapIcon"
	AssignKeyword SnowglobeNellis "MinimapIcon"
	AssignKeyword SnowglobeTheStrip "MinimapIcon"
	AssignKeyword SchematicsDartGunItem "MinimapIcon"
	AssignKeyword SchematicsBottlecapMineItem "MinimapIcon"
	AssignKeyword SchematicsDeathclawGauntletItem "MinimapIcon"
	AssignKeyword SchematicsNukaCocktailItem "MinimapIcon"
	AssignKeyword SchematicsRailwayRifleItem "MinimapIcon"
	AssignKeyword SchematicsRockItLauncherItem "MinimapIcon"
	AssignKeyword SchematicsShishkebabItem "MinimapIcon"
	AssignKeyword SchematicsMantisScytheItem "MinimapIcon"
	AssignKeyword SchematicsMichaelAngeloProp "MinimapIcon"
	AssignKeyword SchematicsPowderChargeItem "MinimapIcon"
	if eval iDeadMoney
		AssignKeyword (EditorIDToFormID "NVDLC01Snowglobe") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01DeanSecretStash") "MinimapIcon"
	endif
	if eval iHonestHearts
		AssignKeyword (EditorIDToFormID "NVDLC02SnowGlobe") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC02DuffleBagFORCHALLENGE") "MinimapIcon"
	endif
	if eval iOldWorldBlues
		AssignKeyword (EditorIDToFormID "NVDLC03SnowGlobe") "MinimapIcon"
	endif
	if eval iLonesomeRoad
		AssignKeyword (EditorIDToFormID "NVDLC04Snowglobe") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC04RALPHIEPosterActivator") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC04Warhead") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC04DivideBldgWarhead") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC04DivideDeathclawWarhead") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC04DivideSniperWarhead01") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC04DivideSniperWarhead02") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC04DivideSniperWarhead03") "MinimapIcon"
	endif
	if eval iTaleOfTwoWastelands
		AssignKeyword (EditorIDToFormID "BobbleheadAGL") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadBART") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadBGUN") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadCHA") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadEND") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadERGW") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadEXPL") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadINT") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadLOCK") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadLUK") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadMEDI") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadMELE") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadPER") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadREPR") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadSCNC") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadSGUN") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadSNEK") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadSPCH") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadSTR") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadSUR") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "BobbleheadUARM") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "DLC01SteelPlateActivator") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "DLC02FF004Briefcase") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "testPhilSchematicsTrenchKnifeItem") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "SchematicsCryolatorItem") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "SchematicsMolotovCocktailItem") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "SchematicsSECtoMPCItem") "MinimapIcon"
	endif
	iTemp += 128
endif

; Assign keywords (Quest)
if eval GetINIFloat_Cached "Objects:bQuest" "Stentorious\ModernMinimap.ini" 1
	;if eval iTaleOfTwoWastelands
	;	AssignKeyword (EditorIDToFormID "MegatonPipeJointY01ACT") "MinimapIcon"
	;	AssignKeyword (EditorIDToFormID "MegatonPipeStraightRegSmlACT") "MinimapIcon"
	;	AssignKeyword (EditorIDToFormID "MegatonPipeJointLrgToLrgACT") "MinimapIcon"
	;endif
	iTemp += 16
endif

; Assign keywords (Trap)
if eval GetINIFloat_Cached "Objects:bTrap" "Stentorious\ModernMinimap.ini" 1
	AssignKeyword TrapGrenadeBunch "MinimapIcon"
	AssignKeyword TrapBearTrap "MinimapIcon"
	AssignKeyword TrapTripwire01 "MinimapIcon"
	AssignKeyword trapShotgun "MinimapIcon"
	AssignKeyword TrapComputer "MinimapIcon"
	AssignKeyword trapMineBuried01 "MinimapIcon"
	AssignKeyword TrapBabyCarriage "MinimapIcon"
	AssignKeyword TrapPressurePlate "MinimapIcon"
	AssignKeyword TrapPitchingMachine01 "MinimapIcon"
	AssignKeyword TrapMailboxBomb01 "MinimapIcon"
	AssignKeyword SprungTrapBearTrap "MinimapIcon"
	AssignKeyword VHDLegionOliverRangerTrapBearTrap "MinimapIcon"
	AssignKeyword VHDLegionOliverTrapTripwire "MinimapIcon"
	if eval iDeadMoney
		AssignKeyword (EditorIDToFormID "NVDLC01TrapTripwire") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01TrapBearTrap") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01TrapPressurePlate") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01trapShotgun") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01SprungTrapBearTrap") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01Radio") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01SpeakerINV") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01SpeakerDest") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC01SpeakerINVOff") "MinimapIcon"
	endif
	if eval iHonestHearts
		AssignKeyword (EditorIDToFormID "NVDLC02MQ03BearTrap") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC02BearTrap") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC02TrapTripwire01") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "NVDLC02TrapPressurePlate") "MinimapIcon"
	endif
	if eval iTaleOfTwoWastelands
		AssignKeyword (EditorIDToFormID "trapShotgunCombat") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "TrapElectrifiedToiletTop01") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "TakomaTrapBabyCarriage") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "DLC04LabTrapComputer") "MinimapIcon"
		AssignKeyword (EditorIDToFormID "DLC04TrapPitchingMachine") "MinimapIcon"
	endif
endif

; Icon visibility bitmask
iTemp += 2 * (eval GetINIFloat_Cached "Objects:bNote" "Stentorious\ModernMinimap.ini" 1)
iTemp += 4 * (eval GetINIFloat_Cached "Objects:bKey" "Stentorious\ModernMinimap.ini" 1)
iTemp += 8 * (eval GetINIFloat_Cached "Objects:bUnique" "Stentorious\ModernMinimap.ini" 1)
iTemp += 32 * (eval GetINIFloat_Cached "Objects:bMine" "Stentorious\ModernMinimap.ini" 1)
iTemp += 64 * (eval GetINIFloat_Cached "Objects:bLockedContainer" "Stentorious\ModernMinimap.ini" 1)
iTemp += 256 * (eval GetINIFloat_Cached "Objects:bTerminal" "Stentorious\ModernMinimap.ini" 1)
iTemp += 512 * (eval GetINIFloat_Cached "Objects:bDoor" "Stentorious\ModernMinimap.ini" 1)

Goo1.AuxVarSetFlt "*Minimap_ySI" iTemp 0
Goo1.AuxVarSetFlt "*Minimap_ySI" (Clamp (GetINIFloat_Cached "Vendors:iMode" "Stentorious\ModernMinimap.ini" 2) 0 2) 1

; Add custom quest marker template
AddTileFromTemplate "HUDMainMenu\ModernMinimap\CustomMarkerLocal|Minimap_Quest_Local_Template|Icon"
AddTileFromTemplate "HUDMainMenu\ModernMinimap\CustomMarkerWorld|Minimap_Quest_World_Template|Icon"
SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerLocal\Icon\depth" 499
SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerWorld\Icon\depth" 499
SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerLocal\Icon\_Custom" 1
SetUIStringAlt "HUDMainMenu\ModernMinimap\CustomMarkerWorld\Icon\filename" "Interface\Stentorious\ModernMinimap\MarkerPlayerAbove.dds"

; Init event handlers
SetOnRenderUpdateEventHandler 1 (CompileScript "ModernMinimap\OnRender.gek") 0
SetGameMainLoopCallback (CompileScript "ModernMinimap\MainLoop.gek") 1 (GetMaxOf 1 (GetINIFloat_Cached "Performance:iUpdateRateLocal" "Stentorious\ModernMinimap.ini" 20)) 1
SetGameMainLoopCallback (CompileScript "ModernMinimap\WorldMap\MainLoop.gek") 1 (GetMaxOf 1 (GetINIFloat_Cached "Performance:iUpdateRateWorld" "Stentorious\ModernMinimap.ini" 300)) 1
SetEventHandler "OnCellExit" (CompileScript "ModernMinimap\CellChange.gek")
SetOnLocationDiscoverEventHandler (CompileScript "ModernMinimap\WorldMap\OnLocationDiscover.gek") 1
SetOnFastTravelEventHandler (begin Function {ref rTemp}
	Goo1.AuxVarSetFlt "*Minimap_Travel" 1
	CallAfterFrames 0 (CompileScript "ModernMinimap\FastTravel.gek") 0 (Player.GPC)
end) 1

; Build world map marker aux vars
CallAfterFrames 0 (CompileScript "ModernMinimap\WorldMap\BuildMarkerArray.gek")

; Clear INI cache
CloseFileSO "Stentorious\ModernMinimap.ini" 0

; Show update message
if eval GetINIFloat "Mods:ModernMinimap" "Stentorious\UpdateMessages.ini" < 200
	CallAfter 1 ({} => MessageBoxEx "Welcome to Modern Minimap 2.0+%r%rThe minimap will now display the local map in interiors and world map in exteriors.%r%rThis behavior can be disabled via the config.%r%rYou can toggle between the local/world map at any time with the %k key." (Goo1.AuxVarGetFlt "*Minimap_Key" 0))
	SetINIFloat "Mods:ModernMinimap" 200 "Stentorious\UpdateMessages.ini"
endif
