int	iDist
int	iTemp
int iSize
int iIndex
int iType
int iBitIcon
ref rIcon
array_var aRefs
array_var aTemp
array_var aMarkerRefs
array_var aFollowers

begin Function {}

	; Update player info
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrDetect" (GetPCDetectionState)

	; Reset tracked refs
	aMarkerRefs = Ar_Construct "array"
	Goo1.AuxVarErase "*Minimap_Doors"
	Goo1.AuxVarErase "*Minimap_Basic"
	Goo1.AuxVarErase "*Minimap_Actors"
	Goo1.AuxVarErase "*Minimap_Followers"
	Goo1.AuxVarErase "*Minimap_Markers"

	; Get global settings
	iDist = GetUIFloatAlt "HUDMainMenu\ModernMinimap\_Dist"
	iBitIcon = Goo1.AuxVarGetFlt "*Minimap_ySI" 0

	; These are split into different sections for performance

	; Door refs
	aRefs = Player.GetRefs 28 2 1 (iDist * 2)
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && (rIcon.IsLoadDoor || rIcon.GetLocked > 0)
				Goo1.AuxVarSetRef "*Minimap_Doors" rIcon -1
			endif
		loop
	endif

	; Container
	aRefs = Player.GetRefs 27 2 1 iDist
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && ((GetBit iBitIcon 6 && rIcon.GetLocked > 0) || HasKeyword rIcon "MinimapIcon")
				Goo1.AuxVarSetRef "*Minimap_Doors" rIcon -1
			endif
		loop
	endif

	; Item refs
	aRefs = Player.GetRefs 201 2 1 iDist
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D
				iType = rIcon.GetType
				if eval (iType == 49 && GetBit iBitIcon 1) || (iType == 46 && GetBit iBitIcon 2) || (iType == 115 && GetBit iBitIcon 7) || (GetBit iBitIcon 4 && rIcon.IsQuestItem) || (GetBit iBitIcon 3 && IsItemUnique (rIcon.gbo)) || HasKeyword rIcon "MinimapIcon"
					Goo1.AuxVarSetRef "*Minimap_Basic" rIcon -1
				endif
			endif
		loop
	endif

	; Activators
	aRefs = Player.GetRefs 21 2 1 iDist
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.GetDestroyed == 0 && HasKeyword rIcon "MinimapIcon"
				Goo1.AuxVarSetRef "*Minimap_Basic" rIcon -1
			endif
		loop
	endif

	; Terminal
	if eval GetBit iBitIcon 8
		aRefs = Player.GetRefs 23 2 1 iDist
		if eval (iSize = Ar_Size aRefs) > 0
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D
					Goo1.AuxVarSetRef "*Minimap_Basic" rIcon -1
				endif
			loop
		endif
	endif

	; Projectile
	if eval GetBit iBitIcon 5
		aRefs = Player.GetRefs 51 2 1 iDist
		if eval (iSize = Ar_Size aRefs) > 0
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.GetMineArmed
					Goo1.AuxVarSetRef "*Minimap_Basic" rIcon -1
				endif
			loop
		endif
	endif

	; Actor refs
	if eval (iTemp = Goo1.AuxVarGetFlt "*Minimap_INI" 3) > 0
		if iTemp == 2
			aRefs = Player.GetRefs 43 2 1 iDist
		else
			aRefs = Player.GetRefs 42 2 1 iDist
			if iTemp == 3
				aTemp = Player.GetRefs 43 2 1 iDist
				ar_Cat aRefs aTemp
			endif
		endif

		if eval (iSize = Ar_Size aRefs) > 0

			; Build array of followers
			aFollowers = Player.GetFollowers
			aTemp = Player.GetTeammates
			if aFollowers
				ar_Cat aFollowers aTemp
			else
				aFollowers = aTemp
			endif
			aFollowers = Ar_Unique aFollowers

			; Checks if actor is a follow and assigns them to the respective aux
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.IsActorsAIOff == 0 && rIcon.IsActorInvisibleToPlayer == 0
					if eval (Ar_Find rIcon aFollowers) == Ar_BadNumericIndex
						Goo1.AuxVarSetRef "*Minimap_Actors" rIcon -1
					else
						Goo1.AuxVarSetRef "*Minimap_Followers" rIcon -1
					endif
				endif
			loop
		endif
	endif

	; Quest objective markers
	aRefs = GetCurrentQuestObjectiveTeleportLinks
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize][0]
			if eval IsReference rIcon
				Ar_Append aMarkerRefs rIcon
			endif
		loop
		aMarkerRefs = Ar_Unique aMarkerRefs
	endif

	; Update custom marker tile
	if eval IsFormValid (rIcon = GetCustomMapMarker)
		Goo1.AuxVarSetRef "*Minimap_Custom" rIcon
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarker\Icon\visible" 1
	else
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarker\Icon\visible" 0
	endif

	; Update door icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Doors\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Doors") - iIndex
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Doors|Minimap_Icon_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Doors\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Doors\Icon:0"
		loop
	endif

	; Update basic icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Basic\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Basic") - iIndex
	iIndex += 25
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Basic|Minimap_Icon_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Basic\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Basic\Icon:0"
		loop
	endif

	; Update actor icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Actors\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Actors") - iIndex
	iIndex += 125
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Actors|Minimap_Icon_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Actors\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Actors\Icon:0"
		loop
	endif

	; Update follower icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Followers\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Followers") - iIndex
	iIndex += 175
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Followers|Minimap_Icon_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Followers\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Followers\Icon:0"
		loop
	endif

	; Update quest marker tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Markers\childcount")
	iSize = (Ar_Size aMarkerRefs) - iIndex
	iIndex += 400
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Markers|Minimap_Marker_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Markers\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Markers\Icon:0"
		loop
	endif
	Goo1.AuxVarSetFromArr "*Minimap_Markers" aMarkerRefs

	aRefs = aTemp = aFollowers = aMarkerRefs = ar_Null

end