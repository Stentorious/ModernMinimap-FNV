int	iDist
int	iTemp
int iSize
int iIndex
int iType
float fPosX
float fPosY
ref rIcon
array_var aRefs
array_var aTemp
array_var aMarkerRefs
array_var aFollowers

begin Function {}

	; Disable tile updates
	if eval GetUIFloatAlt "HUDMainMenu\ModernMinimap\_ToggleUpdate" == 0
		return
	endif

	; Update player info
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrDetect" (GetPCDetectionState)
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrSneak" (Player.IsSneaking)

	; Update custom marker tile
	if eval IsFormValid (rIcon = GetCustomMapMarker)
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_ShowCustom" 1
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerLocal\Icon\_PosX" (rIcon.GetPos X)
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerLocal\Icon\_PosY" (rIcon.GetPos Y)
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerLocal\Icon\_PosZ" (rIcon.GetPos Z)
		rIcon.GetWorldMapPosMults (fPosX) (fPosY)
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerWorld\Icon\_PosX" fPosX
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\CustomMarkerWorld\Icon\_PosY" fPosY
	else
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_ShowCustom" 0
	endif

	; Quest objective markers
	Goo1.AuxVarErase "*Minimap_Markers"
	aMarkerRefs = Ar_Construct "array"
	aRefs = GetCurrentQuestObjectiveTeleportLinks
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			if eval IsReference (rIcon = aRefs[iSize][0])
				Ar_Append aMarkerRefs rIcon
			endif
		loop
		aMarkerRefs = Ar_Unique aMarkerRefs
	endif

	; Update quest marker tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\QuestLocal\childcount")
	iSize = (Ar_Size aMarkerRefs) - iIndex
	iIndex = 400
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\QuestLocal|Minimap_Quest_Local_Template|Icon"
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\QuestWorld|Minimap_Quest_World_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\QuestLocal\Icon:0\_DepthMod" (iIndex += 1)
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\QuestWorld\Icon:0\_DepthMod" (iIndex + 200)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\QuestLocal\Icon:0"
			UnloadUIComponent "HUDMainMenu\ModernMinimap\QuestWorld\Icon:0"
		loop
	endif
	Goo1.AuxVarSetFromArr "*Minimap_Markers" aMarkerRefs

	; Update player world map coordinates
	if eval GetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapMode" == 0
		return
	endif

	; Reset tracked refs
	Goo1.AuxVarErase "*Minimap_Doors"
	Goo1.AuxVarErase "*Minimap_Basic"
	Goo1.AuxVarErase "*Minimap_Actors"
	Goo1.AuxVarErase "*Minimap_Followers"

	; Get global settings
	iDist = GetUIFloatAlt "HUDMainMenu\ModernMinimap\_Dist"

	; These are split into different sections for performance

	; Door refs
	aRefs = Player.GetRefs 28 2 1 (iDist * 2)
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && (rIcon.IsLoadDoor || rIcon.GetLocked > 0)
				Goo1.AuxVarSetRef "*Minimap_Doors" rIcon -1
			endif
		loop
	endif

	; Container
	aRefs = Player.GetRefs 27 2 1 iDist
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && ((Goo1.AuxVarGetFlt "*Minimap_ySI" 9 && rIcon.GetLocked > 0) || AuxVarGetFlt "*MinimapIcon" 0 (rIcon.GBO) || HasKeyword rIcon "MinimapIcon")
				Goo1.AuxVarSetRef "*Minimap_Doors" rIcon -1
			endif
		loop
	endif

	; Item refs
	aRefs = Player.GetRefs 201 2 1 iDist
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D
				iType = rIcon.GetType
				if eval (iType == 49 && Goo1.AuxVarGetFlt "*Minimap_ySI" 3) || (iType == 46 && Goo1.AuxVarGetFlt "*Minimap_ySI" 4) || (iType == 115 && Goo1.AuxVarGetFlt "*Minimap_ySI" 6) || (Goo1.AuxVarGetFlt "*Minimap_ySI" 7 && rIcon.IsQuestItem) || (Goo1.AuxVarGetFlt "*Minimap_ySI" 5 && IsItemUnique (rIcon.gbo)) || AuxVarGetFlt "*MinimapIcon" 0 (rIcon.GBO) || HasKeyword rIcon "MinimapIcon"
					Goo1.AuxVarSetRef "*Minimap_Basic" rIcon -1
				endif
			endif
		loop
	endif

	; Activators
	aRefs = Player.GetRefs 21 2 1 iDist
	if eval (iSize = Ar_Size aRefs) > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.GetDestroyed == 0 && (AuxVarGetFlt "*MinimapIcon" 0 (rIcon.GBO) || HasKeyword rIcon "MinimapIcon")
				Goo1.AuxVarSetRef "*Minimap_Basic" rIcon -1
			endif
		loop
	endif

	; Terminal
	if eval Goo1.AuxVarGetFlt "*Minimap_ySI" 10
		aRefs = Player.GetRefs 23 2 1 iDist
		if eval (iSize = Ar_Size aRefs) > 0
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D
					Goo1.AuxVarSetRef "*Minimap_Basic" rIcon -1
				endif
			loop
		endif
	endif

	; Projectile
	if eval Goo1.AuxVarGetFlt "*Minimap_ySI" 12
		aRefs = Player.GetRefs 51 2 1 iDist
		if eval (iSize = Ar_Size aRefs) > 0
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.GetMineArmed
					Goo1.AuxVarSetRef "*Minimap_Basic" rIcon -1
				endif
			loop
		endif
	endif

	; Actor refs
	if eval Goo1.AuxVarGetFlt "*Minimap_INI" 2 || Goo1.AuxVarGetFlt "*Minimap_INI" 3
		if eval Goo1.AuxVarGetFlt "*Minimap_INI" 2
			aRefs = Player.GetRefs 42 2 1 iDist
		endif
		if eval Goo1.AuxVarGetFlt "*Minimap_INI" 3
			if eval Goo1.AuxVarGetFlt "*Minimap_INI" 2
				aTemp = Player.GetRefs 43 2 1 iDist
				ar_Cat aRefs aTemp
			else
				aRefs = Player.GetRefs 43 2 1 iDist
			endif
		endif

		if eval (iSize = Ar_Size aRefs) > 0

			; Build array of followers
			aFollowers = Player.GetFollowers
			aTemp = Player.GetTeammates
			if aFollowers
				ar_Cat aFollowers aTemp
			else
				aFollowers = aTemp
			endif
			aFollowers = Ar_Unique aFollowers

			; Checks if actor is a follow and assigns them to the respective aux
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.IsActorsAIOff == 0 && rIcon.IsActorInvisibleToPlayer == 0
					if eval (Ar_Find rIcon aFollowers) == Ar_BadNumericIndex
						Goo1.AuxVarSetRef "*Minimap_Actors" rIcon -1
					else
						Goo1.AuxVarSetRef "*Minimap_Followers" rIcon -1
					endif
				endif
			loop
		endif
	endif

	; Update door icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Doors\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Doors") - iIndex
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Doors|Minimap_Icon_Local_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Doors\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Doors\Icon:0"
		loop
	endif

	; Update basic icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Basic\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Basic") - iIndex
	iIndex += 25
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Basic|Minimap_Icon_Local_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Basic\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Basic\Icon:0"
		loop
	endif

	; Update actor icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Actors\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Actors") - iIndex
	iIndex += 125
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Actors|Minimap_Icon_Local_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Actors\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Actors\Icon:0"
		loop
	endif

	; Update follower icon tiles
	iIndex = GetMaxOf 0 (GetUIFloatAlt "HUDMainMenu\ModernMinimap\Followers\childcount")
	iSize = (Goo1.AuxVarSize "*Minimap_Followers") - iIndex
	iIndex += 175
	if iSize > 0
		while (iSize -= 1) > -1
			AddTileFromTemplate "HUDMainMenu\ModernMinimap\Followers|Minimap_Icon_Local_Template|Icon"
			SetUIFloatAlt "HUDMainMenu\ModernMinimap\Followers\Icon:0\_DepthMod" (iIndex += 1)
		loop
	elseif iSize < 0
		while (iSize += 1) < 1
			UnloadUIComponent "HUDMainMenu\ModernMinimap\Followers\Icon:0"
		loop
	endif

	; Update player path
	if eval (iSize = (GetUIFloatAlt "HUDMainMenu\ModernMinimap\PlayerPath\childcount" - 1)) > -1
		if eval GetUIFloatAlt ("HUDMainMenu\ModernMinimap\PlayerPath\Icon:" + $iSize + "\alpha") == 0
			UnloadUIComponent ("HUDMainMenu\ModernMinimap\PlayerPath\Icon:" + $iSize)
		endif
	endif
	fPosX = GetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrX"
	fPosY = GetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrY"
	if eval (fPosX - (GetUIFloatAlt "HUDMainMenu\ModernMinimap\PlayerPath\Icon:0\_PosX"))^2 + (fPosY - (GetUIFloatAlt "HUDMainMenu\ModernMinimap\PlayerPath\Icon:0\_PosY"))^2 > 8100
		AddTileFromTemplate "HUDMainMenu\ModernMinimap\PlayerPath|Minimap_Player_Path_Template|Icon"
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\PlayerPath\Icon:0\_PosX" fPosX
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\PlayerPath\Icon:0\_PosY" fPosY
		SetUIFloatGradual "HUDMainMenu\ModernMinimap\PlayerPath\Icon:0\_Fade" 510 0 3
	endif

	aRefs = aTemp = aFollowers = aMarkerRefs = ar_Null

end