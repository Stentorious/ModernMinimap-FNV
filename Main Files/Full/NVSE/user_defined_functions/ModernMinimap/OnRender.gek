int iType
int iSize
int iCorpse
int iVendor
int iDoor
int iState
float fPosX
float fPosY
float fAngle
ref rIcon
ref rTemp
string_var sUIPath
string_var sFilename

begin Function {}

	; Disable render updates
	if eval GetUIFloatAlt "HUDMainMenu\ModernMinimap\_ToggleUpdate" == 0
		return
	endif

	; Update map rotation
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrDeg" (Player.GetAngle Z)

	; Update world map
	if eval GetUIFloatAlt "HUDMainMenu\ModernMinimap\_MapMode" == 0
		if eval GetUIFloatAlt "HUDMainMenu\ModernMinimap\_Interior" == 1
			return
		endif

		; Update player coordinates
		Player.GetWorldMapPosMults (fPosX) (fPosY)
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldX" fPosX
		SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrWldY" fPosY

		; Update quest markers
		if eval (iSize = Goo1.AuxVarSize "*Minimap_Markers") > 0
			while (iSize -= 1) > -1
				if eval IsReference (rIcon = Goo1.AuxVarGetRef "*Minimap_Markers" iSize) == 0
					continue
				endif
				sUIPath = Sv_Construct "HUDMainMenu\ModernMinimap\QuestWorld\Icon:%g\" iSize
				rIcon.GetWorldMapPosMults (fPosX) (fPosY)
				SetUIFloatAlt (sUIPath + "_PosX") fPosX
				SetUIFloatAlt (sUIPath + "_PosY") fPosY
			loop
		endif
		return
	endif

	; Update player position
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrX" (Player.GetPos X)
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrY" (Player.GetPos Y)
	SetUIFloatAlt "HUDMainMenu\ModernMinimap\_PlrZ" (Player.GetPos Z)


	; Update door/container icons
	if eval (iSize = Goo1.AuxVarSize "*Minimap_Doors") > 0

		iDoor = Goo1.AuxVarGetFlt "*Minimap_ySI" 8

		while (iSize -= 1) > -1
			if eval IsReference (rIcon = Goo1.AuxVarGetRef "*Minimap_Doors" iSize) == 0
				continue
			endif

			iType = rIcon.GetType

			; Assign icon based on lock status
			if eval rIcon.GetLocked > 0 && (iType == 27 || (iType == 28 && iDoor))
				if eval rIcon.PlayerHasKey
					sFilename = "interface\icons\Misc_Keychain.dds"
				elseif eval rIcon.GetLockLevel > 100 || rIcon.GetLocked == 2
					sFilename = "interface\icons\Misc_Key.dds"
				else
					sFilename = "interface\icons\Misc_BobbyPin.dds"
				endif

			; Assign interior/exterior door icon
			else
				sFilename = rIcon.ySIGetTrait "filename"
				if eval iType == 28 && rIcon.IsLoadDoor && rIcon.IsInInterior
					rTemp = rIcon.GetTeleportDoor
					if eval IsReference rTemp && rTemp.IsInInterior == 0
						sFilename = "interface\icons\Misc_Door_Ext.dds"
					endif
				endif
			endif

			; Update UI traits
			sUIPath = Sv_Construct "HUDMainMenu\ModernMinimap\Doors\Icon:%g\" iSize
			if eval iType == 28
				SetUIFloatAlt (sUIPath + "_Dist") ((Player.GetDistance3D rIcon) / 2)
			else
				SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
			endif
			SetUIFloatAlt (sUIPath + "_PosX") (rIcon.GetPos X)
			SetUIFloatAlt (sUIPath + "_PosY") (rIcon.GetPos Y)
			SetUIFloatAlt (sUIPath + "_PosZ") (rIcon.GetPos Z)
			SetUIStringAlt (sUIPath + "filename") (sFilename)

		loop
	endif


	; Update basic icons
	if eval (iSize = Goo1.AuxVarSize "*Minimap_Basic") > 0
		while (iSize -= 1) > -1
			if eval IsReference (rIcon = Goo1.AuxVarGetRef "*Minimap_Basic" iSize) == 0
				continue
			endif
			sUIPath = Sv_Construct "HUDMainMenu\ModernMinimap\Basic\Icon:%g\" iSize
			SetUIFloatAlt (sUIPath + "_PosX") (rIcon.GetPos X)
			SetUIFloatAlt (sUIPath + "_PosY") (rIcon.GetPos Y)
			SetUIFloatAlt (sUIPath + "_PosZ") (rIcon.GetPos Z)
			SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
			SetUIStringAlt (sUIPath + "filename") (rIcon.ySIGetTrait "filename")
		loop
	endif


	; Update actor icons
	if eval (iSize = Goo1.AuxVarSize "*Minimap_Actors") > 0

		; Get actor settings
		iCorpse = Goo1.AuxVarGetFlt "*Minimap_INI" 4
		iVendor = Goo1.AuxVarGetFlt "*Minimap_ySI" 0

		; Process refs and update any valid icons
		while (iSize -= 1) > -1
			if eval IsReference (rIcon = Goo1.AuxVarGetRef "*Minimap_Actors" iSize) == 0
				continue
			endif

			iState = -1
			fAngle = -1

			; Dead icons
			if eval rIcon.GetDead
				if eval iCorpse == 0 || (iCorpse == 1 && (Ar_Size (rIcon.GetAllItems 0 1 0 0)) == -1 && (Ar_Size (rIcon.GetDroppedRefs)) == -1)
					iState = -2
				else
					sFilename = "interface\icons\Misc_Skull.dds"
				endif

			; Vendor icons
			elseif eval rIcon.GetType == 42 && iVendor > 0 && rIcon.GetMerchantContainer
				if eval iVendor == 1
					sFilename = "interface\icons\Misc_BottleCap.dds"
				elseif eval GetServiceFlag (rIcon.GBO) 131072
					sFilename = "interface\icons\Aid_WeaponRepairKit.dds"
				elseif eval ySIGetTrait "tag" (rTemp = rIcon.GetClass) != ""
					sFilename = ySIGetTrait "filename" rTemp
				else
					sFilename = "interface\icons\Misc_BottleCap.dds"
				endif

			; Basic icon
			else
				fAngle = rIcon.GetAngle Z
				iState = rIcon.IsCompassHostile
				sFilename = "Interface\Stentorious\ModernMinimap\Actor.dds"
			endif

			; Update UI traits
			sUIPath = Sv_Construct "HUDMainMenu\ModernMinimap\Actors\Icon:%g\" iSize
			SetUIFloatAlt (sUIPath + "_PosX") (rIcon.GetPos X)
			SetUIFloatAlt (sUIPath + "_PosY") (rIcon.GetPos Y)
			SetUIFloatAlt (sUIPath + "_PosZ") (rIcon.GetPos Z)
			SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
			SetUIFloatAlt (sUIPath + "_Deg") fAngle
			SetUIFloatAlt (sUIPath + "_Hostile") iState
			SetUIStringAlt (sUIPath + "filename") (sFilename)

		loop
	endif


	; Update follower icons
	if eval (iSize = Goo1.AuxVarSize "*Minimap_Followers") > 0

		; Get actor settings
		iCorpse = Goo1.AuxVarGetFlt "*Minimap_INI" 4

		while (iSize -= 1) > -1
			if eval IsReference (rIcon = Goo1.AuxVarGetRef "*Minimap_Followers" iSize) == 0
				continue
			endif

			iState = -1
			fAngle = -1

			; Give corpse icon or hide if dead
			if eval rIcon.GetDead
				if eval iCorpse == 0 || (iCorpse == 1 && (Ar_Size (rIcon.GetAllItems 0 1 0 0)) == -1 && (Ar_Size (rIcon.GetDroppedRefs)) == -1)
					iState = -2
				else
					sFilename = "interface\icons\Misc_Skull.dds"
				endif
			elseif eval rIcon.GetKnockedState
				sFilename = "interface\icons\Aid_DoctorBag.dds"
				iState = 1
			elseif eval Goo1.AuxVarGetFlt "*JIPCCC_Avatars" && Goo1.AuxVarGetFlt "*Minimap_INI" 5 && rIcon.GetPlayerTeammate && (sFilename = AuxStringMapGetStr "*JIPCCC_Avatars" (GetName rIcon)) != ""
				if eval Goo1.AuxVarGetFlt "*Minimap_INI" 6
					iState = 2
				else
					iState = 3
				endif
			else
				iState = 2
				fAngle = rIcon.GetAngle Z
				sFilename = "Interface\Stentorious\ModernMinimap\Actor.dds"
			endif

			; Update UI traits
			sUIPath = Sv_Construct "HUDMainMenu\ModernMinimap\Followers\Icon:%g\" iSize
			SetUIFloatAlt (sUIPath + "_PosX") (rIcon.GetPos X)
			SetUIFloatAlt (sUIPath + "_PosY") (rIcon.GetPos Y)
			SetUIFloatAlt (sUIPath + "_PosZ") (rIcon.GetPos Z)
			SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
			SetUIFloatAlt (sUIPath + "_Deg") fAngle
			SetUIFloatAlt (sUIPath + "_Hostile") iState
			SetUIStringAlt (sUIPath + "filename") (sFilename)

		loop
	endif


	; Update quest markers
	if eval (iSize = Goo1.AuxVarSize "*Minimap_Markers") > 0
		while (iSize -= 1) > -1
			if eval IsReference (rIcon = Goo1.AuxVarGetRef "*Minimap_Markers" iSize) == 0
				continue
			endif
			sUIPath = Sv_Construct "HUDMainMenu\ModernMinimap\QuestLocal\Icon:%g\" iSize
			SetUIFloatAlt (sUIPath + "_PosX") (rIcon.GetPos X)
			SetUIFloatAlt (sUIPath + "_PosY") (rIcon.GetPos Y)
			SetUIFloatAlt (sUIPath + "_PosZ") (rIcon.GetPos Z)
		loop
	endif

	sv_Destruct sUIPath sFilename

end