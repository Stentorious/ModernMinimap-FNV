int iType
int iSize
int iTemp
int iCorpse
int iVendor
int iHostile
int iTaleOfTwoWastelands
float fDist
float fAngle
ref rIcon
ref rTemp
ref rVendorChems
ref rVendorChemsFood
string_var sUIPath
string_var sFilename
array_var aIconRefs

begin Function {}

	; Update player position
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_PlrX" (Player.GetPos X)
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_PlrY" (Player.GetPos Y)
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_PlrZ" (Player.GetPos Z)
	SetUIFloatAlt "HUDMainMenu\SimpleMinimap\_PlrDeg" (Player.GetAngle Z)

	; Update actor/door/item icons
	aIconRefs = Goo1.AuxVarGetAsArr "*Minimap_Icons"
	iSize = Ar_Size aIconRefs
	if eval iSize > 0

		; Get actor settings
		iCorpse = Goo1.AuxVarGetFlt "*Minimap_INI" 4
		iVendor = Goo1.AuxVarGetFlt "*Minimap_INI" 6
		if eval Goo1.AuxVarGetFlt "*TaleOfTwoWastelands.esm"
			iTaleOfTwoWastelands = 1
			rVendorChems = EditorIDToFormID "VendorChems"
			rVendorChemsFood = EditorIDToFormID "VendorChemsFood"
		endif

		; Process refs and update any valid icons
		while (iSize -= 1) > -1
			rIcon = aIconRefs[iSize]
			if IsFormValid rIcon && IsReference rIcon

				sUIPath = Sv_Construct "HUDMainMenu\SimpleMinimap\Icons\Icon:%g\" iSize
				iType = rIcon.GetType
				iHostile = -1
				fAngle = -1
				fDist = Player.GetDistance3D rIcon

				; Actor icons
				if eval iType == 42 || iType == 43

					; Dead icons
					if eval rIcon.GetDead
						if eval iCorpse && rIcon.GetContainerInventoryCount == 0
							iHostile = -2
						else
							sFilename = "interface\icons\Misc_Skull.dds"
						endif

					; ySI framework icon
					elseif eval rIcon.ySIGetTrait "tag" != ""
						sFilename = rIcon.ySIGetTrait "filename"

					; Vendor icons
					elseif eval iType == 42 && iVendor > 0 && rIcon.GetMerchantContainer
						rTemp = rIcon.GetClass
						if iVendor == 1
							sFilename = "interface\icons\Misc_BottleCap.dds"
						elseif eval GetServiceFlag (rIcon.GBO) 131072
							sFilename = "interface\icons\Aid_WeaponRepairKit.dds"
						elseif eval rTemp == VendorDrinks
							sFilename = "interface\icons\Aid_Alcohol.dds"
						elseif eval rTemp == Doctor
							sFilename = "interface\icons\Aid_DoctorBag.dds"
						elseif eval rTemp == VendorWeapons
							sFilename = "interface\icons\Ammo_Bullet.dds"
						elseif eval rTemp == VendorFood
							sFilename = "interface\icons\Aid_CookedMeal.dds"
						elseif eval rTemp == VendorClothes
							sFilename = "interface\icons\Apparel_Other_Bag.dds"
						elseif eval iTaleOfTwoWastelands && (rTemp == rVendorChems || rTemp == rVendorChemsFood)
							sFilename = "interface\icons\Aid_Drug.dds"
						else
							sFilename = "interface\icons\Misc_BottleCap.dds"
						endif

					; Basic Icon
					else
						fAngle = rIcon.GetAngle Z
						iHostile = rIcon.IsCompassHostile
						sFilename = "Interface\Stentorious\SimpleMinimap\Actor.dds"
					endif

				; Door, Container, and Terminal icons
				elseif eval iType == 28 || iType == 27 || iType == 23

					if eval iType == 28
						fDist /= 2
					endif

					if eval rIcon.GetLocked > 0

						; Has key
						if eval rIcon.PlayerHasKey
							sFilename = "interface\icons\Misc_Keychain.dds"
						; Locked
						else
							iTemp = rIcon.GetLockLevel
							if eval iType != 23
								iTemp /= 25
							endif
							; Requires key
							if eval iTemp > 4 || rIcon.GetLocked == 2
								sFilename = "interface\icons\Misc_Key.dds"
							; Pickable
							else
								sFilename = "interface\icons\Misc_BobbyPin.dds"
							endif
						endif

					else
						sFilename = rIcon.ySIGetTrait "filename"
						if eval iType == 28 && rIcon.IsLoadDoor && rIcon.IsInInterior
							rTemp = rIcon.GetTeleportDoor
							if eval IsFormValid rTemp && rTemp.IsInInterior == 0
								sFilename = "interface\icons\Misc_Door_Ext.dds"
							endif
						endif
					endif

				; All other icons
				else
					sFilename = rIcon.ySIGetTrait "filename"
				endif

				; Update UI traits
				SetUIFloatAlt (sUIPath + "_PosX") (rIcon.GetPos X)
				SetUIFloatAlt (sUIPath + "_PosY") (rIcon.GetPos Y)
				SetUIFloatAlt (sUIPath + "_PosZ") (rIcon.GetPos Z)
				SetUIFloatAlt (sUIPath + "_Dist") fDist
				SetUIFloatAlt (sUIPath + "_Deg") fAngle
				SetUIFloatAlt (sUIPath + "_Hostile") iHostile
				SetUIStringAlt (sUIPath + "filename") (sFilename)

			endif
		loop
	endif

	; Update quest markers
	aIconRefs = Goo1.AuxVarGetAsArr "*Minimap_Markers"
	iSize = Ar_Size aIconRefs
	if eval iSize > 0
		while (iSize -= 1) > -1
			rIcon = aIconRefs[iSize]
			if IsFormValid rIcon && IsReference rIcon
				sUIPath = Sv_Construct "HUDMainMenu\SimpleMinimap\Markers\Marker:%g\" iSize
				SetUIFloatAlt (sUIPath + "_PosX") (rIcon.GetPos X)
				SetUIFloatAlt (sUIPath + "_PosY") (rIcon.GetPos Y)
				SetUIFloatAlt (sUIPath + "_PosZ") (rIcon.GetPos Z)
			endif
		loop
	endif

	; Update custom marker
	rIcon = Goo1.AuxVarGetRef "*Minimap_Custom"
	if IsFormValid rIcon && IsReference rIcon
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\CustomMarker\_PosX" (rIcon.GetPos X)
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\CustomMarker\_PosY" (rIcon.GetPos Y)
		SetUIFloatAlt "HUDMainMenu\SimpleMinimap\CustomMarker\_PosZ" (rIcon.GetPos Z)
	endif

	sv_Destruct sUIPath sFilename
	aIconRefs = Ar_Null

end